/*******************************************************************************************
 *                              gradle file : ROOT
 ******************************************************************************************/
group 'org.mixit'
version '2.0-SNAPSHOT'

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

buildscript {
    ext {
        springRepoM = 'http://repo.spring.io/libs-milestone'
        springRepo = 'http://repo.spring.io/libs-release-remote'

        vAssertJ = '3.1.0'
        vDbsetup = '1.6.0'
        vGuava = '18.0'
        vH2 = '1.3.170'
        vJava = '1.8'
        vJunit = '4.12'
        vJUnitParams = '1.0.4'
        vFlyway = '3.2.1'
        vMockito = '1.10.19'
        vNodePlugin = '0.10'
        vSl4j = '1.7.12'
        vSpringBoot = '1.3.0.M5'
        vSpringLoaded = '1.2.4.RELEASE'
        vSwagger = '2.0.3'
    }

    repositories {
        maven { url springRepoM }
        maven { url springRepo }
    }

    dependencies {
        classpath "com.moowork.gradle:gradle-gulp-plugin:${vNodePlugin}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${vSpringBoot}"
        classpath "org.springframework:springloaded:${vSpringLoaded}"
        classpath "org.flywaydb:flyway-gradle-plugin:${vFlyway}"

        classpath "com.h2database:h2:${vH2}"
    }
}


/*******************************************************************************************
 * The different plugins used in submodules and dependencies are defined here
 ******************************************************************************************/

//Default IDE is IntelliJ and we use the jar packaging
configure(allprojects) {
    apply plugin: 'idea'
    apply plugin: 'java'

    targetCompatibility = vJava
    sourceCompatibility = vJava
}

//We use gulp to manage the webapp
configure([ project(':client-ng1'), project(':client-ng2')]) {
    apply plugin: 'com.moowork.node'
}

//Flyway is used to manage the database
configure([ project(':common')]) {
    apply plugin: 'org.flywaydb.flyway'
}

//Uses Spring Boot
configure([ project(':server')]) {
    apply plugin: 'spring-boot'

    repositories {
        maven { url springRepo }
    }

    dependencies {

        compile "org.springframework.boot:spring-boot-starter-web:${vSpringBoot}"
        compile "org.springframework.boot:spring-boot-starter-hateoas:${vSpringBoot}"
        compile "org.springframework.boot:spring-boot-starter-data-jpa:${vSpringBoot}"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.6.1"
        compile "com.google.guava:guava:${vGuava}"
        compile "org.slf4j:slf4j-api:${vSl4j}"
        compile "com.h2database:h2:${vH2}"
        compile "io.springfox:springfox-swagger2:${vSwagger}"
        compile "io.springfox:springfox-swagger-ui:${vSwagger}"

        testCompile "junit:junit:${vJunit}"
        testCompile "pl.pragmatists:JUnitParams:${vJUnitParams}"
        testCompile "org.assertj:assertj-core:${vAssertJ}"
        testCompile "org.mockito:mockito-core:${vMockito}"
        testCompile "org.springframework:spring-test"
        testCompile "com.ninja-squad:DbSetup:${vDbsetup}"
    }

    configurations {
        all*.exclude group: 'commons-logging', module: 'commons-logging'
        all*.exclude group: 'log4j', module: 'log4j'
    }

}
